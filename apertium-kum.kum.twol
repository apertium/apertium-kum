Alphabet

 А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я

 %{A%}:а %{A%}:е
 %{I%}:ы %{I%}:и %{I%}:у %{I%}:ю
 %{D%}:д 
 %{N%}:н
 %{M%}:б %{M%}:м
 %{G%}:г %{G%}:к
 %{E%}:а %{E%}:е %{E%}:й
 %{r%}:р
 %{s%}:с
 %{n%}:н
 %{l%}:л %{l%}:н
 %{ъ%}:ъ %{ъ%}:0

 %{❗%}:0
 %{☭%}:0
 
 %>:0
 % 
 %+   ;

Sets

Vow = а э и о у А Э И О У     ! Уь Оь 
      я е ы ё ю Я Е Ы Ё Ю  ;  ! уь Оь

Cns =  Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь
       б в г д ж з й к л м н п р с т ф х ц ч ш щ ъ ь ; 

Rules

"Deletion of {n} word final"
%{n%}:0 <=>  _ (:0) .#. ;

"Deletion of {r} before suffix" ! FIXME
%{r%}:0 <=> _ %>: ; 
        except 
            _ %>: (:0) [ %{D%}: | %{I%}: ] ;

"Deletion of {s} after a consonant"
%{s%}:0 <=> [ :Cns | й: ]/[ %>: | :0 ] _ ;

"Deletion of {I} after vowels"
%{I%}:0 <=> [ :Vow ]/[ :0 | %>: ] _ ;

"Deletion of ь when vowel-initial stem follows"
! е behaves like a consonant after vowels; i.e., it's /йэ/
ь:0 <=> _ %>: [ :Vow - :е ]/[ :0 ] ;
!       except
!             [ :а | :о | :у ] _ %>: [ :Vow ]/[ :0 ] ;
    except
        _ й: %>: %{E%}: ;

"Deletion of accusative and genitive {D} after {n}"
%{N%}:0 <=> %{n%}: %>: _ %{I%}: .#. ;
            %{n%}: %>: _ %{I%}: .#. ;

"Intervocalic voicing of п/к"
Cx:Cy <=> :Vow (:0) _ ([ :0 - %{❗%}:0 ]) %>: (:0) :Vow ;
          except
            %{❗%}:0 _ ;
                    _ :0* %{I%}: :п ;
          where Cx in ( п к )
                Cy in ( б г )
                matched;

"Deletion of {A} after vowels"
%{A%}:0 <=> [ :Vow ]/[ [ :0 - й:0 ] | %>: ] _ ;

